!# devices.fn 1.3 CCU.IO
!#
!# Dieses Homematic-Script gibt eine Liste aller Geraete als JSON String aus
!#
!# 3'2013-7'2013 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;
string sDPId;

Write('{');

    boolean dFirst = true;

    foreach (sDevId, root.Devices().EnumUsedIDs()) {


    object oDevice   = dom.GetObject(sDevId);
    boolean bDevReady = oDevice.ReadyConfig();
    string sDevInterfaceId = oDevice.Interface();
    string sDevInterface   = dom.GetObject(sDevInterfaceId).Name();

    if (bDevReady) {

        if (dFirst) {
          dFirst = false;
        } else {
          WriteLine(',');
        }

        Write('"' # sDevId # '":{"Name":"');
        WriteURL(oDevice.Name());
        Write('","TypeName":"' # oDevice.TypeName());
        Write('","HssType":"' # oDevice.HssType() # '","Address":"' # oDevice.Address() # '","Interface":"' # sDevInterface);
        Write('","Channels":[');

        string sChnId;

        boolean cFirst = true;
        foreach(sChnId, oDevice.Channels()) {
            if (cFirst) {
                cFirst = false;
            } else {
                Write(',');
            }
            Write(sChnId);
        }
        Write(']}');
    }
}
Write('}');
